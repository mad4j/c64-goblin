TODO velocizzare procedura di definizione dei caratteri
TODO rendere il codice piÃ¹ leggibile
TODO aggiungere colori al campo di gioco
TODO modificare lo sprite del folletto
90 REM ...
100 REM MAIN TITLE
110 POKE 53280,2 : REM SET BORDER COLOR
120 POKE 53281,1 : REM SET BACKGR COLOR
130 PRINT "{CLEAR}"
140 PRINT "{DOWN*5}{RIGHT*4}{GREEN}GOBLIN {RED}{REVERSE ON}RELOADED"
150 PRINT "{DOWN*2}{RIGHT*4}{BLUE}DANIELE.OLMISANI@GMAIL.COM"
160 PRINT "{DOWN*2}{RIGHT*4}USE O,P KEYS TO MOVE"
170 PRINT "{DOWN*2}{RIGHT*4}PLEASE WAIT... DEFINING CHARACTERS"
175 REM ...
180 REM INITIALIZATION
190 POKE 52,48 : POKE 56,48 : CLR
195 CM = 12288 : REM CUSTOM CHAR MAP START ADDRESS
198 OM = 53248 : REM ORIGINAL CHAR MAP START ADDRESS
199 GF = 54272 : REM GAME FIELD START ADDRESS
200 POKE 56334,PEEK(56334) AND 254 : REM TURN INTERRUPTS OFF
210 POKE 1,PEEK(1) AND 251 : REM SWITCH I/O WITH CHARS ROM
220 FOR N=384 TO 464 : REM COPY DIGIT CHARS
230 : POKE N+CM,PEEK(N+OM)
240 NEXT N
250 IF S>HS THEN HS=S
260 RESTORE : B=4 : Z=1964 : Z1=Z+GF : W=0 : S=J : G=0
270 VS=54296 : AD=54277 : SR=54278 : WF=54276 : LB=GF : HB=54273
280 FOR X=0 TO 39
290  : READ A
300  : POKE X+CM,A
310 NEXT X
320 POKE 1,PEEK(1)OR 4 :
330 POKE 56334,PEEK(56334)OR 1 : REM TURN INTERRUPTS ON
340 POKE 53272,(PEEK(53272) AND 240)+12
350 PRINT"{CLEAR}"
360 PRINT"{HOME}{RED}{DOWN*2}{SPACE*17}"HS
370 FOR I=1 TO 118 : REM RANDOM BLOCKS
380  : X=INT(RND(1)*680)+1144
390  : IF PEEK(X)=B THEN 380
400  : POKE X,B : REM PUT BLOCK IN POSITION X
410  : POKE X+GF,5 : REM PUT CHANGE COLOR OF POSITION X
420 NEXT I
430 FOR I=1 TO 36
440  : G1=0
450  : X=INT(RND(1)*680)+1144
460  : IF PEEK(X)=B OR PEEK(X)=1 OR PEEK(X)=3 THEN 450
470  : IF PEEK(X+39)=B AND PEEK(X+40)=B AND PEEK(X+41)=B THEN POKE X,3 : POKE X+GF,0 : G1=1
480  : IF G1=1 THEN G=G+1 : GOTO 500
490  : POKE X,1 : POKE X+GF,8
500 NEXT I
510 POKE Z,32 : Z=Z-40 : Z1=Z1-40 : IF Z<1144 THEN Z=Z+760 : Z1=Z1+760
520 GET A$ : IF A$="O" THEN Z=Z-1 : Z1=Z1-1
530 IF A$="P" THEN Z=Z+1 : Z1=Z1+1
540 IF PEEK(Z)=B THEN 670
550 IF PEEK(Z)=1 THEN GOSUB 590
560 POKE Z,0 : POKE Z1,0 : FOR T=1 TO 220 : NEXT
570 IF W=36-G THEN J=S : GOSUB 610 : GOTO 250
580 GOTO 510
590 W=W+1 : S=S+25 : PRINT"{HOME}{BLUE}{DOWN*2}"S : POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,17
600 POKE HB,71 : POKELB,12 : FOR T=1 TO 90 : NEXT T : POKE VS,0 : POKE HB,0 :POKE LB,0 : RETURN
610 PRINT"{HOME}{RED}{DOWN*18}{RIGHT*8}{REVERSE OFF}****** ALL RIGHT !******"
620 FOR I=1 TO 10 : GET C$ : NEXT I : REM COLLECT GARBAGE
630 POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,17 : FOR I=1 TO 17
640 H=INT(RND(0)*10)+21 : L=INT(RND(0)*45)+210 : POKE HB,H : POKE LB,L
650 FOR T=1 TO 80 : NEXT T : NEXT I : POKE VS,0 : POKE HB,0 : POKE LB,0
660 RETURN
670 POKE Z,2 : POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,129 : POKE HB,2 : POKE LB,125
680 FOR I=1 TO 400 : NEXT I : POKE VS,15 : POKE HB,0 : POKE LB,0
690 FOR X=1144 TO 1823 : IF PEEK(X)<>1 THEN NEXT X
700 IF PEEK(X)=1 THEN POKE X,3 : NEXT X
710 J=0
720 FOR I=1 TO 10 : GET C$ : NEXT I
730 PRINT"{HOME}{BLUE}{DOWN*20}{REVERSE ON}PLAY AGAIN? (Y/N)" : POKE 646,14
740 GET C$ : IF C$="" THEN GOTO 740
750 IF C$="Y" THEN 250
760 POKE 53272,21 : POKE 53280,14 : POKE 53281,6 : POKE 52,50 : POKE 56,50 : PRINT"{CLEAR)SEE YA!"
770 DATA 126,219,219,255,165,90,90,165
777 DATA 60,66,165,129,153,165,66,60
780 DATA 170,85,170,85,126,219,255,189
785 DATA 60,66,165,129,165,153,66,60
790 DATA 170,85,170,85,170,85,170,85