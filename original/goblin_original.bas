80 POKE 53280,2 : POKE 53281,1
90 PRINT"{CLEAR}{DOWN*7}{RIGHT*4}PLEASE WAIT. . .DEFINING CHARACTERS";
100 POKE 52,48 : POKE 56,48 : CLR : POKE 56334,PEEK(56334) AND 254
105 POKE 1,PEEK(1) AND 251
108 FOR N=0 TO 2047 : POKE N+12288,PEEK(N+53248) : NEXT N
109 FOR N=0 TO 7 : POKE N+12320,PEEK(N+54064) : NEXT N
110 IF S>HS THEN HS=S
112 RESTORE : B=4 : Z=1964 : Z1=Z+54272 : W=0 : S=J : G=0
115 VS=54296 : AD=54277 : SR=54278 : WF=54276 : LB=54272 : HB=54273
120 FOR X=0 TO 31 : READ A : POKE X+12288,A : NEXT
123 POKE 1,PEEK(1)OR 4 : POKE 56334,PEEK(56334)OR 1
125 POKE 53272,(PEEK(53272) AND 240)+12
130 PRINT"{CLEAR}{GREEN}{RIGHT*14}{REVERSE ON}G O B L I N"
140 PRINT"{HOME}{RED}{DOWN*2}{REVERSE OFF}" SPC(17) "HS="HS
145 PRINT"{HOME}{BLACK}{DOWN*22}{REVERSE ON}O=LEFT"; SPC(27); "P=RIGHT"
150 FOR I=1 TO 118
160 X=INT(RND(1)*680)+1144
170 IF PEEK(X)=B THEN 160
180 POKE X,B : POKE X+54272,0 : NEXT I
190 FOR I=1 TO 36
195 G1=0
200 X=INT(RND(1)*680)+1144
210 IF PEEK(X)=B OR PEEK(X)=1 OR PEEK(X)=3 THEN 200
220 IF PEEK(X+39)=B AND PEEK(X+40)=B AND PEEK(X+41)=B THEN POKE X,3 : POKEX+54272,0 : G1=1
225 IF G1=1 THEN G=G+1 : GOTO 240
230 POKE X,1 : POKE X+54272,0
240 NEXT I
250 POKE Z,32 : Z=Z-40 : Z1=Z1-40 : IF Z<1144 THEN Z=Z+760 : Z1=Z1+760
260 GET A$ : IF A$="O" THEN Z=Z-1 : Z1=Z1-1
270 IF A$="P" THEN Z=Z+1 : Z1=Z1+1
280 IF PEEK(Z)=B THEN 410
290 IF PEEK(Z)=1 THEN GOSUB 330
300 POKE Z,0 : POKE Z1,0 : FOR T=1 TO 220 : NEXT
310 IF W=36-G THEN J=S : GOSUB 350 : GOTO 110
320 GOTO 250
330 W=W+1 : S=S+25 : PRINT"{HOME}{BLUE}{DOWN*2}"S : POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,17
340 POKE HB,71 : POKELB,12 : FOR T=1 TO 90 : NEXT T : POKE VS,0 : POKE HB,0 :POKE LB,0 : RETURN
350 PRINT"{HOME}{RED}{DOWN*18}{RIGHT*8}{REVERSE OFF}****** ALL RIGHT !******"
355 FOR I=1 TO 10 : GET C$ : NEXT I : REM COLLECT GARBAGE
360 POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,17 : FOR I=1 TO 17
370 H=INT(RND(0)*10)+21 : L=INT(RND(0)*45)+210 : POKE{SPACE}HB,H : POKE LB,L
380 FOR T=1 TO 80 : NEXT T : NEXT I : POKE VS,0 : POKE HB,0 : POKE LB,0
400 RETURN
410 POKE Z,2 : POKE VS,15 : POKE AD,30 : POKE SR,200 : POKE WF,129 : POKE HB,2 : POKE LB,125
415 FOR I=1 TO 400 : NEXT I : POKE VS,15 : POKE HB,0 : POKE LB,0
420 FOR X=1144 TO 1823 : IF PEEK(X)<>1 THEN NEXT X
430 IF PEEK(X)=1 THEN POKE X,3 : NEXT X
440 J=0
445 FOR I=1 TO 10 : GET C$ : NEXT I
450 PRINT"{HOME}{BLUE}{DOWN*20}{REVERSE ON}PLAY AGAIN? (Y/N)" : POKE 646,14
465 GET C$ : IF C$="" THEN 465
470 IF C$="Y" THEN 110
490 POKE 53272,21 : POKE 53280,14 : POKE 53281,6 : POKE 52,50 : POKE 56,50 : PRINT"{CLEAR)SEE YA!"
500 DATA 126,219,219,255,165,90,90,165,60,66,165,129,153,165,66,60
510 DATA 170,85,170,85,126,219,255,189,60,66,165,129,165,153,66,60
520 DATA 0,0,0,0,0,0,0,0
